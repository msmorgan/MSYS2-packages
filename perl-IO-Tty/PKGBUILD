_realname=IO-Tty
pkgname=perl-${_realname}
pkgver=1.12
pkgrel=1
pkgdesc="Provide an interface to TTYs and PTYs"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/${_realname}/"
license=("GPL" "PerlArtistic")
depends=() #('glibc')
makedepends=('libcrypt-devel')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TODDR/${_realname}-$pkgver.tar.gz")
sha256sums=('a2ef8770d3309178203f8c8ac25e623e63cf76e97830fd3be280ade1a555290d')

build() {
  cd "$srcdir"/${_realname}-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}
package() {
  cd "$srcdir"/${_realname}-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
